{
{-# LANGUAGE BangPatterns #-}
-- The above pragma is a workaround inserted in the Alex source for this parser
-- to fix a bug in Alex 2.3.5.  Alex 2.3.5 uses bang patterns when -g is
-- enabled but does not set this language option.  Cabal uses Alex with -g, so
-- the above is necessary for source generated by Alex 2.3.5 to compile.
{-# OPTIONS_GHC -w #-}

module Language.BigBang.Syntax.Lexer
( Token(..)
, lexBigBang
) where
}

%wrapper "basic"

$digit = 0-9
$lowerAlpha = [a-z]
$upperAlpha = [A-Z]
$alpha = [$lowerAlpha $upperAlpha]

tokens :-

    $white+                             ;
    `                                   { const TokLabelPrefix }
    &                                   { const TokOnionCons }
    \\                                  { const TokLambda }
    fun                                 { const TokFun }
    \->                                 { const TokArrow }
    case                                { const TokCase }
    of                                  { const TokOf }
    int                                 { const TokInteger }
    char                                { const TokChar }
    unit                                { const TokUnit }
    \(                                  { const TokOpenParen }
    \)                                  { const TokCloseParen }
    \-?$digit+                          { TokIntegerLiteral . read }
    \' ( \\. | ~\' ) \'                 { TokCharLiteral . head . tail }
    $alpha [$alpha $digit _ ']*         { TokIdentifier }
    \{                                  { const TokOpenBlock }
    \}                                  { const TokCloseBlock }
    \;                                  { const TokSeparator }
    _                                   { const TokUnder }
    :                                   { const TokColon }

{
lexBigBang :: String -> [Token]
lexBigBang = alexScanTokens

data Token =
      TokLabelPrefix
    | TokOnionCons
    | TokLambda
    | TokFun
    | TokArrow
    | TokCase
    | TokOf
    | TokInteger
    | TokChar
    | TokUnit
    | TokUnder
    | TokOpenParen
    | TokCloseParen
    | TokIntegerLiteral Integer
    | TokCharLiteral Char
    | TokIdentifier String
    | TokOpenBlock
    | TokCloseBlock
    | TokSeparator
    | TokColon
    deriving (Eq, Show)
}
    
