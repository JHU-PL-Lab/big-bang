#+TITLE: Big Bang User Documentation
#+AUTHOR: Johns Hopkins University Programming Languages Laboratory

* Phylosophy
** Target user
   Seasoned scripter (someone familiar with Python).
* Features
** Literals
*** Primitive
**** Booleans
***** True
#+BEGIN_SRC big-bang
true
#+END_SRC
***** False
#+BEGIN_SRC big-bang
false
#+END_SRC
**** Numbers
***** Integers (~int~)
#+BEGIN_SRC big-bang
32
-10
1_000_000
#+END_SRC
**** Characters
#+BEGIN_SRC big-bang
'a'
#+END_SRC
**** Texts
#+BEGIN_SRC big-bang
"Hello, Big Bang."
#+END_SRC
**** Empty onion
#+BEGIN_SRC big-bang
()
#+END_SRC
*** Structured
**** Records
#+BEGIN_SRC big-bang
{ a = 2, b = 3 }
#+END_SRC
**** Lists
#+BEGIN_SRC big-bang
[2, "Lists are heterogeneous!", 'a']
#+END_SRC
**** Labels
#+BEGIN_SRC big-bang
`LanguageName "Big Bang"
#+END_SRC
** Operations
*** Primitive
**** Booleans
***** ~and~
***** ~or~
***** ~xor~
***** ~not~
**** Numbers
***** ~+~
***** ~-~
***** ~*~
***** ~/~
**** Texts
***** Indexing
#+BEGIN_SRC big-bang
"abc"(1) # => 'b'
#+END_SRC
*** Structured
**** Onioning
#+BEGIN_SRC big-bang
3 & 'a'
#+END_SRC
***** In case of a name key collision, left side has wins
**** Records
***** Selection
#+BEGIN_SRC big-bang
{ a = 2, b = 3 }.a # => 2
#+END_SRC
**** Lists
***** Indexing
#+BEGIN_SRC big-bang
[2, 3, 'a'](2) # => 'a'
#+END_SRC
*** General
**** Comparison
***** ~=?~
***** ~not=?~
** Identifiers
#+BEGIN_SRC big-bang
inGrid?()
changeGrid!()
x'''''
x's
#+END_SRC
*** ~​'​~, ~?~ and ~!~ may appear anywhere in the identifier (except for the first character)
*** Only one namespace for variables and functions (Lisp-1)
** Assignments
*** Immutable
#+BEGIN_SRC big-bang
let banana = "banana"
#+END_SRC
*** Mutable
**** First definition
#+BEGIN_SRC big-bang
ref coconut = "banana"
#+END_SRC
**** Update
#+BEGIN_SRC big-bang
coconut = "banana2"
#+END_SRC
**** Dereferencing is automatic (i.e. doesn't require ~!~)
#+BEGIN_SRC big-bang
functionCall(coconut)
#+END_SRC
*** Shadowing
**** Not allowed (except with function parameters)
** Flow control constructs
*** Sequencing
**** Expressions may be sequenced when separated by the divisor (new line, ~;~, ~then~ or ~do~)
*** Conditionals
**** ~if~
#+BEGIN_SRC big-bang
if <condition>
  <expression>
elseif <condition>
  <expression>
else
  <expression>
end
#+END_SRC
**** Pattern matches
#+BEGIN_SRC big-bang
match <expression>
as <pattern> in <expression>
as <pattern> in <expression>
end
#+END_SRC
*** Repetitions
**** While
#+BEGIN_SRC big-bang
repeat while <condition>
  <expression>
end
#+END_SRC
** Functions
*** Definition
**** Named
#+BEGIN_SRC big-bang
fun <name>(<parameter-list>) =
  <expression>
end
#+END_SRC
**** Anonymous
#+BEGIN_SRC big-bang
fun (<parameter-list>) =
  <expression>
end
#+END_SRC
**** No difference between non-recursive and recursive functions
**** ~return~ is allowed (parsed), but isn't handled yet (an error is raised)
*** Application
#+BEGIN_SRC big-bang
identity(3)
#+END_SRC
** Object orientation
*** Objects
#+BEGIN_SRC big-bang
object
  include
    SomeOtherObject
  public
    x = 0
    y = 0

    # `self` is optional
    def magnitude() = sqrt(self.x * self.x + y * y) end
end
#+END_SRC
*** Method call
#+BEGIN_SRC big-bang
foo.bar()
#+END_SRC
** Standard library
*** Characters
**** ~ord('a')~
**** ~chr(64)~
*** I/O
**** Read one char
**** Print one char
** Documentation
*** Comments
**** ~#~
** Code style
*** Indentation is 2 spaces
*** Trailing commas
**** Structured data literals
**** Function call parameters
*** Identifiers
**** CamelCase
*** ~;~, ~then~ and ~do~ work as new line separators
*** Empty blocks are allowed on the following expressions (~`Void ()~ is returned)
#+BEGIN_SRC big-bang
fun () = end # => Anonymous function that returns `Void ().
#+END_SRC
**** Flow control constructs
***** Conditionals
****** ~if~
****** Pattern matches
***** Loops
**** Function definitions
**** Objects
**** Method definitions
* Roadmap
** Short-term
*** Literals
**** Sets
**** Maps
*** Functions
**** Properly encode ~return~
** Long-term
*** Exceptions
*** Regex
* Grammar
#+BEGIN_SRC ebnf
program =
  | expression eof

expression =
  | literal-expression
  | binary-operator-expression
  | unary-prefix-operator-expression
  | application-or-indexing-expression
  | selection-expression
  | assignment-expression
  | flow-control-expression
  | function-expression
  | return-expression
  | object-expression
  | method-call-expression

pattern =
  | literal-expression
  | tiny-bang-patterns
  | catch-all-pattern

catch-all-pattern = "_"

literal-expression =
  | primitive-literal
  | structured-literal

primitive-literal =
  | boolean-literal
  | number-literal
  | character-literal
  | text-literal
  | empty-onion-literal

structured-literal =
  | record-literal
  | list-literal
  | label-literal

boolean-literal =
 | "true"
 | "false"

number-literal =
  | integer-literal

integer-literal =
  "-"? (
    | ["1"-"9"] ["0"-"9" "_"]*
    | "0"
  )

character-literal = ...

text-literal = ...

empty-onion-literal = "()"

record-literal = "{" list-of(element = record-field, separator = ",") "}"

record-field = identifier "=" expression

list-literal = "[" list-of(element = list-field, separator = ",") "]"

list-field =
  | expression

label-literal = label-identifier expression

label-identifier = "`" identifier-continuation+

binary-operator-expression =
  | expression binary-operator expression

binary-operator =
  | primitive-binary-operator
  | structured-binary-operator
  | general-binary-operator

primitive-binary-operator =
  | boolean-binary-operator
  | number-binary-operator

structured-binary-operator =
  | onioning-operator

general-binary-operator =
  | comparison-operator

boolean-binary-operator = ["and" "or" "xor"]

number-binary-operator = ["+" "-" "*" "/"]

onioning-operator = "&"

comparison-operator = ["=?" "not=?"]

unary-prefix-operator-expression =
  | unary-prefix-operator expression

unary-prefix-operator =
  | primitive-unary-prefix-operator

primitive-unary-prefix-operator =
  | boolean-unary-prefix-operator

boolean-unary-prefix-operator = ["not"]

# This production conflates the syntax for function application and indexing in
# texts and lists. They are spelled the same.
application-or-indexing-expression =
  | expression actual-parameters-or-indexes

actual-parameters-or-indexes =
  "(" list-of(element = actual-parameter-or-index, separator = ",") ")"

actual-parameter-or-index =
  | expression

# Selection works on records and objects (for fields, not methods).
selection-expression = expression "." identifier

identifier = identifier-start identifier-continuation*

identifier-start = ["A"-"Z" "a"-"z" "_"]

identifier-continuation = ["A"-"Z" "a"-"z" "_" "0"-"9" "?" "!" "'"]

assignment-expression =
  | immutable-assignment-expression
  | mutable-assignment-expression
  | mutable-update-assignment-expression

immutable-assignment-expression = "let" identifier "=" expression

mutable-assignment-expression = "ref" identifier "=" expression

mutable-update-assignment-expression = identifier "=" expression

flow-control-expression =
  | sequencing-expression
  | conditional-expression
  | repetition-expression

sequencing-expression = expression divider expression

conditional-expression =
  | if-expression
  | pattern-match-expression

if-expression =
  "if" expression divider expression?
  (("elseif" expression divider expression?)* "else" expression?)? "end"

pattern-match-expression =
  "match" expression ("as" pattern "in" expression?)+ "end"

repetition-expression =
  "repeat" (
    | while-expression
  )

while-expression =
  "while" expression divider expression? "end"

function-expression =
  | named-function-expression
  | anonymous-function-expression

named-function-expression =
  "fun" identifier formal-parameters "=" expression? "end"

anonymous-function-expression = "fun" formal-parameters "=" expression? "end"

formal-parameters = "(" list-of(element = formal-parameter, separator = ",") ")"

formal-parameter =
  | identifier

return-expression = "return" expression

object-expression = "object" list-of(element = object-section) "end"

object-section =
  | object-include-section
  | object-member-section

object-include-section = "include" object-include-section-body

object-include-section-body = list-of(element = expression)

object-member-section = object-member-section-header divider object-member-section-body

object-member-section-header = ["public" "private"]

object-member-section-body = list-of(element = object-member)

object-member =
  | object-field-definition
  | method-definition

object-field-definition =
  | immutable-assignment-expression
  | mutable-assignment-expression

method-definition =
  | "def" identifier formal-parameters "=" expression? "end"

method-call-expression = expression "." identifier actual-parameters-or-indexes

comment = "#" .*? $

new-line = ["\n" "\r" "\r\n"]

divider =
  | ["then" "do" ";"]
  | new-line

list-of(
  element,
  separator = divider,
  possibly-empty? = true,
  possibly-trailing-separator? = true
) = ...
#+END_SRC
