#+TITLE: Big Bang User Documentation
#+AUTHOR: Johns Hopkins University Programming Languages Laboratory

* Phylosophy
** Target user
   Seasoned scripter (someone familiar with Python).
* Features
** Literals
*** Primitive data
**** Booleans
***** True
#+BEGIN_SRC big-bang
true
#+END_SRC
***** False
#+BEGIN_SRC big-bang
false
#+END_SRC
**** Numbers
***** Integers (~int~)
#+BEGIN_SRC big-bang
32
-10
1_000_000
#+END_SRC
**** Characters
#+BEGIN_SRC big-bang
'a'
#+END_SRC
**** Texts
#+BEGIN_SRC big-bang
"Hello, Big Bang."
#+END_SRC
**** Empty onion
#+BEGIN_SRC big-bang
()
#+END_SRC
*** Structured data
**** Onions
#+BEGIN_SRC big-bang
3 & 'a'
#+END_SRC
***** In case of a name key collision, left side has wins
**** Records
#+BEGIN_SRC big-bang
{ a = 2, b = 3 }
#+END_SRC
**** Lists
#+BEGIN_SRC big-bang
[2, "Lists are heterogeneous!", 'a']
#+END_SRC
**** Labels
#+BEGIN_SRC big-bang
`LanguageName "Big Bang"
#+END_SRC
** Operations
*** Primitive data types
**** Booleans
***** ~and~
***** ~or~
***** ~xor~
***** ~not~
**** Numbers
***** ~+~
***** ~-~
***** ~/~
***** ~*~
**** Texts
***** Indexing
#+BEGIN_SRC big-bang
"abc"(1) # => 'b'
#+END_SRC
*** Structured data types
**** Records
***** Selection
#+BEGIN_SRC big-bang
{ a = 2, b = 3 }.a # => 2
#+END_SRC
**** Lists
***** Indexing
#+BEGIN_SRC big-bang
[2, 3, 'a'](2) # => 'a'
#+END_SRC
*** General
**** Comparison
***** ~=?~
***** ~not=?~
** Identifiers
#+BEGIN_SRC big-bang
inGrid?()
changeGrid!()
x'''''
x's
#+END_SRC
*** ~​'​~, ~?~ and ~!~ may appear anywhere in the identifier (except for the first character)
*** Only one namespace for variables and functions (Lisp-1)
** Assignments
*** Immutable
#+BEGIN_SRC big-bang
let banana = "banana"
#+END_SRC
*** Mutable
**** First definition
#+BEGIN_SRC big-bang
ref coconut = "banana"
#+END_SRC
**** Update
#+BEGIN_SRC big-bang
coconut = "banana2"
#+END_SRC
**** Dereferencing is automatic (i.e. doesn't require ~!~)
#+BEGIN_SRC big-bang
functionCall(coconut)
#+END_SRC
*** Shadowing
**** Not allowed (except with function parameters)
** Flow control constructs
*** Conditionals
**** ~if~
#+BEGIN_SRC big-bang
if <condition>
  <expression>
elseif <condition>
  <expression>
else
  <expression>
end
#+END_SRC
**** Pattern matches
#+BEGIN_SRC big-bang
match <expression>
as <pattern> in <expression>
as <pattern> in <expression>
end
#+END_SRC
*** Loops
#+BEGIN_SRC big-bang
repeat while <condition>
  <expression>
end
#+END_SRC
** Functions
*** Definition
**** Named
#+BEGIN_SRC big-bang
fun <name>(<parameter-list>) =
  <expression>
end
#+END_SRC
**** Anonymous
#+BEGIN_SRC big-bang
fun (<parameter-list>) =
  <expression>
end
#+END_SRC
**** No difference between non-recursive and recursive functions
**** ~return~ is allowed (parsed), but isn't handled yet (an error is raised)
*** Application
#+BEGIN_SRC big-bang
identity(3)
#+END_SRC
** Object orientation
*** Objects
#+BEGIN_SRC big-bang
object
  include
    SomeOtherObject
  public
    x = 0
    y = 0

    # `self` is optional
    def magnitude() = sqrt(self.x * self.x + y * y) end
end
#+END_SRC
** Standard library
*** Characters
**** ~ord('a')~
**** ~chr(64)~
*** I/O
**** Read one char
**** Print one char
** Documentation
*** Comments
**** ~#~
** Code style
*** Indentation is 2 spaces
*** Trailing commas
**** Structured data literals
**** Function call parameters
*** Identifiers
**** CamelCase
*** ~;~, ~then~ and ~do~ work as new line separators
*** Empty blocks are allowed on the following expressions (~`Void ()~ is returned)
#+BEGIN_SRC big-bang
fun () = end # => Anonymous function that returns `Void ().
#+END_SRC
**** Flow control constructs
***** Conditionals
****** ~if~
****** Pattern matches
***** Loops
**** Function definitions
**** Objects
**** Method definitions
* Roadmap
** Short-term
*** Literals
**** Sets
**** Maps
*** Functions
**** Properly encode ~return~
** Long-term
*** Exceptions
*** Regex
* Grammar
#+BEGIN_SRC ebnf
program =
  | expression eof

literal =
  | primitive-literal
  | composite-literal

primitive-literal =
  | boolean-literal
  | int-literal
  | character-literal
  | text-literal
  | empty-onion-literal

composite-literal =
  | record-literal
  | list-literal
  | label-literal

boolean-literal =
 | "true"
 | "false"

int-literal =
  | "-"? ["1"-"9"] ["0"-"9" "_"]*
  | "-"? "0"

character-literal = ...

text-literal = ...

empty-onion-literal = "()"

record-literal = "{" list-of(element = record-field, separator = ",") "}"

record-field = identifier "=" expression

list-literal = "[" list-of(element = list-field, separator = ",") "]"

list-field =
  | expression

label-literal = label-identifier expression

label-identifier = "`" identifier-continuation+

expression =
  | unary-prefix-operator-expression
  | binary-operator-expression
  | assignment-expression
  | definition-expression
  | conditional-expression
  | repeat-expression
  | sequence-expression
  | function-expression
  | object-expression
  | application-expression
  | return-expression

definition-expression =
  | immutable-definition-expression
  | mutable-definition-expression

unary-prefix-operator-expression =
  | unary-prefix-operator expression

unary-prefix-operator = ["not"]

binary-operator-expression =
  | expression binary-operator expression

binary-operator = ["&" "+" "-" "/" "*" "and" "or" "xor" "=?" "not=?"]

sequence-expression = expression divider expression

list-of(
  element,
  separator = divider,
  possibly-empty? = true,
  possibly-trailing-separator? = true
) = ...

identifier = identifier-start identifier-continuation*

identifier-start = ["A"-"Z" "a"-"z" "_"]

identifier-continuation =
  | ["A"-"Z" "a"-"z" "_" "0"-"9" "?" "!" "'"]

comment = "#" .*? $

immutable-definition-expression = "let" assignment-expression

mutable-definition-expression = "ref" assignment-expression

assignment-expression = identifier "=" expression

conditional-expression =
  | if-expression
  | match-expression

if-expression =
  "if" expression divider expression? (("elseif" expression divider expression?)* "else" expression?)? "end"

new-line = ["\n" "\r" "\r\n"]

divider =
  | ["then" "do" ";"]
  | new-line

match-expression =
  "match" expression ("as" pattern "in" expression?)+ "end"

repeat-expression =
  "repeat" (
    | while-expression
  )

while-expression =
  "while" expression divider expression? "end"

function-expression =
  | named-function-expression
  | anonymous-function-expression

named-function-expression = "fun" identifier formal-parameters "=" expression? "end"

anonymous-function-expression = "fun" formal-parameters "=" expression? "end"

formal-parameters = "(" list-of(element = formal-parameter, separator = ",") ")"

formal-parameter =
  | identifier

application-expression =
  | expression actual-parameters

return-expression = "return" expression

actual-parameters = "(" list-of(element = actual-parameter, separator = ",") ")"

actual-parameter =
  | expression

object-expression = "object" list-of(element = object-section) "end"

object-section =
  | object-include-section
  | object-member-section

object-include-section = "include" object-include-section-body

object-include-section-body = list-of(element = expression)

object-member-section = object-member-section-header object-member-section-body

object-member-section-header = ["public" "private"]

object-member-section-body = list-of(element = object-member)

object-member =
  | definition-expression
  | method-expression

method-expression =
  | "def" identifier formal-parameters "=" expression? "end"

pattern =
  | literal
  | tiny-bang-patterns
  | "_"
#+END_SRC
