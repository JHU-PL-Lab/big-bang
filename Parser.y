{
module Parser
( parseBigBang
) where

import qualified Ast as A
import qualified Lexer as L
import qualified Types as T
import UtilTypes (Ident, ident, unIdent, LabelName, labelName, unLabelName)

-- For debugging purposes only
import System.IO.Unsafe
import System.IO
}

%name parseBigBang
%tokentype { L.Token }
%error { parseError }

%token
        '`'             { L.TokLabelPrefix }
        '&'             { L.TokOnionCons }
        '\\'            { L.TokLambda }
        fun             { L.TokFun }
        '->'            { L.TokArrow }
        case            { L.TokCase }
        of              { L.TokOf }
        int             { L.TokInteger }
        string          { L.TokString }
        unit            { L.TokUnit }
        '('             { L.TokOpenParen }
        ')'             { L.TokCloseParen }
        intLit          { L.TokIntegerLiteral $$ }
        stringLit       { L.TokStringLiteral $$ }
        ident           { L.TokIdentifier $$ }
        '{'             { L.TokOpenBlock }
        '}'             { L.TokCloseBlock }
        ';'             { L.TokSeparator }

%right      '->'
%right      '&'

%%

Exp     :   '\\' ident '->' Exp
                                    { A.Func (ident $2) $4 }
        |   fun ident '->' Exp
                                    { A.Func (ident $2) $4 }
        |   case Exp of '{' Branches '}'
                                    { A.Case $2 $5 }
        |   Exp '&' Exp
                                    { A.Onion $1 $3 }
        |   ApplExp
                                    { $1 }


ApplExp :   ApplExp Primary
                                    { A.Appl $1 $2 }
        |   Primary
                                    { $1 }


Primary :   ident
                                    { A.Var (ident $1) }
        |   intLit
                                    { A.PrimInt $1 }
        |   stringLit
                                    { A.PrimString $1 }
        |   '(' ')'
                                    { A.PrimUnit }
        |   '`' ident Primary
                                    { A.Label (labelName $2) $3 }
        |   '(' Exp ')'
                                    { $2 }


Branches:   Branch ';' Branches     { $1:$3 }
        |   Branch                  { [$1] }


Branch  :   Pattern '->' Exp        { ($1,$3) }


Pattern :   PrimitiveType           { A.ChiPrim $1 }
        |   '`' ident ident         { A.ChiLabel (labelName $2) (ident $3) }
        |   ident '&' ident         { A.ChiOnion (ident $1) (ident $3) }
        |   fun                     { A.ChiFun }


PrimitiveType
        :   int                     { T.PrimInt }
        |   string                  { T.PrimString }
        |   unit                    { T.PrimUnit }


{
parseError :: [L.Token] -> a
parseError _ = error "Parse error"
}
